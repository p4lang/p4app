/* -*- P4_16 -*- */
#include <core.p4>
#include <v1model.p4>

const bit<16> TYPE_IPV4 = 0x800;
const bit<8> IP_PROT_UDP = 0x11;
const bit<8> ITCH41_MSG_ADD_ORDER = 65; // A

/*************************************************************************
*********************** H E A D E R S  ***********************************
*************************************************************************/

typedef bit<9>  egressSpec_t;
typedef bit<48> macAddr_t;
typedef bit<32> ip4Addr_t;

header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    bit<8>    diffserv;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

header udp_t {
    bit<16> srcPort;
    bit<16> dstPort;
    bit<16> length_;
    bit<16> checksum;
}

header moldudp_hdr_t {
    bit<80> session;
    bit<64> seq;
    bit<16> msg_cnt;
}

header moldudp_msg_t {
    bit<16> msg_len;
}


header itch_msg_type_t {
    bit<8> msg_type;
}

header itch_add_order_t {
    bit<16> stock_locate;
    bit<16> tracking_number;
    bit<48> timestamp_ns;
    bit<64> order_ref_number;
    bit<8> buy_sell_indicator;
    bit<32> shares;
    bit<64> stock;
    bit<32> price;
}

struct metadata {
    /* empty */
}

struct headers {
    ethernet_t        ethernet;
    ipv4_t            ipv4;
    udp_t             udp;
    moldudp_hdr_t     mold_hdr;
    moldudp_msg_t     mold_msg;
    itch_msg_type_t   itch_msg_type;
    itch_add_order_t  add_order;
}

/*************************************************************************
*********************** P A R S E R  ***********************************
*************************************************************************/

parser MyParser(packet_in packet,
                out headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

    state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        transition select(hdr.ipv4.protocol) {
            IP_PROT_UDP: parse_udp;
            default: accept;
        }
    }

    state parse_udp {
        packet.extract(hdr.udp);
        transition select(hdr.udp.dstPort) {
            1234: parse_moldudp;
            default : accept;
        }
    }

    state parse_moldudp {
        packet.extract(hdr.mold_hdr);
        packet.extract(hdr.mold_msg);
        packet.extract(hdr.itch_msg_type);
        transition select(hdr.itch_msg_type.msg_type) {
            ITCH41_MSG_ADD_ORDER: parse_add_order;
            default: accept;
        }
    }
    
    state parse_add_order {
        packet.extract(hdr.add_order);
        transition accept;
    }

}

/*************************************************************************
************   C H E C K S U M    V E R I F I C A T I O N   *************
*************************************************************************/

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {   
    apply {  }
}


/*************************************************************************
**************  I N G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

#include "itch_camus.p4"
control MyIngress(inout headers hdr,
                  inout metadata meta,
                  inout standard_metadata_t standard_metadata) {
    action drop() {
        mark_to_drop();
    }

    action ipv4_forward(egressSpec_t port) {
        standard_metadata.egress_spec = port;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
    }

    table ipv4_lpm {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            ipv4_forward;
            drop;
            NoAction;
        }
        size = 1024;
        default_action = drop();
    }

    action direction_is_down() { }
    action direction_is_up() { }

    table packet_direction {
        key = {
            standard_metadata.ingress_port: exact;
        }
        actions = {
            direction_is_down;
            direction_is_up;
        }
        size = 128;
        default_action = direction_is_up();
    }

    table forward_up {
        actions = {
            ipv4_forward;
            NoAction;
        }
        default_action = NoAction();
    }

    apply {
        if (hdr.ipv4.isValid()) {
            if (hdr.ipv4.ttl == 0) {
                drop();
            }
            else {
                switch(packet_direction.apply().action_run) {
                    direction_is_down: {
                        if (hdr.add_order.isValid())
                            Camus.apply(hdr, standard_metadata);
                        else
                            ipv4_lpm.apply();
                    }
                    direction_is_up: {
                        forward_up.apply();
                    }
                }
            }
        }
    }
}

/*************************************************************************
****************  E G R E S S   P R O C E S S I N G   *******************
*************************************************************************/

control MyEgress(inout headers hdr,
                 inout metadata meta,
                 inout standard_metadata_t standard_metadata) {

    action set_dst(macAddr_t mac, ip4Addr_t ip) {
        hdr.ethernet.dstAddr = mac;
        hdr.ipv4.dstAddr = ip;
        hdr.udp.checksum = 0;
    }

    table rewrite_dst {
        key = {
            standard_metadata.egress_port: exact;
        }
        actions = {
            set_dst;
            NoAction;
        }
        size = 1024;
        default_action = NoAction();
    }

    apply {
        if (hdr.ethernet.isValid() && hdr.ipv4.isValid())
            rewrite_dst.apply();
    }
}

/*************************************************************************
*************   C H E C K S U M    C O M P U T A T I O N   **************
*************************************************************************/

control MyComputeChecksum(inout headers  hdr, inout metadata meta) {
     apply {
	update_checksum(
	    hdr.ipv4.isValid(),
            { hdr.ipv4.version,
	      hdr.ipv4.ihl,
              hdr.ipv4.diffserv,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

/*************************************************************************
***********************  D E P A R S E R  *******************************
*************************************************************************/

control MyDeparser(packet_out packet, in headers hdr) {
    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
        packet.emit(hdr.udp);
        packet.emit(hdr.mold_hdr);
        packet.emit(hdr.mold_msg);
        packet.emit(hdr.itch_msg_type);
        packet.emit(hdr.add_order);
    }
}

/*************************************************************************
***********************  S W I T C H  *******************************
*************************************************************************/

V1Switch(
MyParser(),
MyVerifyChecksum(),
MyIngress(),
MyEgress(),
MyComputeChecksum(),
MyDeparser()
) main;
